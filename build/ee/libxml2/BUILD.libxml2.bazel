load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@kong_bindings//:variables.bzl", "KONG_VAR")

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

configure_make(
    name = "libxml2",
    configure_command = "configure",
    configure_in_place = True,
    configure_options = [
        "--enable-static=no",
        # configure a miminal feature set at first so that we don't
        # end up depend to a lot of dependencies; do not when turning
        # on any of the feature below, we need to add it o kong package's
        # dependencies, and compile it (under build/cross_deps) for
        # cross build platforms
        "--without-http",
        "--without-iconv",
        "--without-python",
        "--without-lzma",
    ] + select({
        "@kong//:aarch64-linux-anylibc-cross": [
            "--host=aarch64-linux",
        ],
        "@kong//:x86_64-linux-musl-cross": [
            "--host=x86_64-linux-musl",
        ],
        "//conditions:default": [],
    }),
    lib_source = ":all_srcs",
    linkopts = select({
        "@kong//:any-cross": [
            "-Wl,-rpath-link,$$EXT_BUILD_DEPS$$/zlib/lib",
        ],
        "//conditions:default": [],
    }),
    out_include_dir = "include/libxml2",
    out_shared_libs = select({
        "@platforms//os:macos": [
            "libxml2.2.dylib",
        ],
        "//conditions:default": [
            "libxml2.so.2.12.5",
        ],
    }),
    targets = [
        "-j" + KONG_VAR["NPROC"],
        "install -j" + KONG_VAR["NPROC"],
    ],
    visibility = ["//visibility:public"],
    deps = select({
        "@kong//:any-cross": [
            "@cross_deps_zlib//:zlib",
        ],
        "//conditions:default": [],
    }),
)
